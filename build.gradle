plugins {
    id 'io.spring.dependency-management' version '1.1.7'
    id 'java'
    id 'jacoco'
    id "com.github.node-gradle.node" version "2.2.1"
    id "org.openapi.generator" version "7.12.0"
    id "org.springframework.boot" version "3.4.2" apply false
}

import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

apply from: 'project-properties.gradle'

def scriptsUrl = 'https://raw.githubusercontent.com/reportportal/gradle-scripts/' +
        (releaseMode ? getProperty('scripts.version') : 'develop')

apply from: 'ui.gradle'
apply from: scriptsUrl + '/release-fat.gradle'
apply from: scriptsUrl + '/signing.gradle'

repositories {
    mavenCentral { url "https://repo1.maven.org/maven2" }
    maven { url "https://packages.atlassian.com/maven/repository/public" }

    if (!releaseMode) {
        maven { url 'https://jitpack.io' }
    }
}

dependencyManagement {
    imports {
        mavenBom(releaseMode ? 'com.epam.reportportal:commons-bom:5.12.1' : 'com.epam.reportportal:commons-bom:5.12.1')
    }
}
ext['spring-boot.version'] = "${springBootVersion}"

dependencies {
    // ALL new dependencies should be included in shadowJar plugin!
    // transitive dependencies may be required as well. could be checked in runtime only!
    if (releaseMode) {
        implementation 'com.epam.reportportal:commons-dao'
        implementation 'com.epam.reportportal:plugin-api'
        annotationProcessor 'com.epam.reportportal:plugin-api'
    } else {
        implementation 'com.github.reportportal:commons-dao:6298e53'
        implementation 'com.github.reportportal:plugin-api:8874441'
        annotationProcessor 'com.github.reportportal:plugin-api:8874441'
    }

    implementation("org.apache.httpcomponents.client5:httpclient5:5.4.2") // supports gzip encoding
    implementation "org.hibernate.validator:hibernate-validator:8.0.2.Final"
    implementation 'jakarta.el:jakarta.el-api:6.0.1'
    implementation 'org.glassfish:jakarta.el:4.0.2'
    implementation "org.springdoc:springdoc-openapi-common:1.8.0"

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    testImplementation('org.junit.jupiter:junit-jupiter:5.11.4')
    testImplementation "org.mockito:mockito-junit-jupiter:5.14.2"

}

sourceSets {
    main {
        java {
            srcDirs "$buildDir/generated/src/main/java"
            exclude("$buildDir/generated/src/main/java/com/epam/reportportal/api/jira/MyselfApi.java")
        }
    }
}


tasks.register('generateJiraApi', GenerateTask) {
    def f = new File("swagger-v3.v3.json")
    if (!f.exists()) {
        new URI("https://developer.atlassian.com/cloud/jira/platform/swagger-v3.v3.json")
                .toURL()
                .withInputStream { i -> f.withOutputStream { it << i } }
    }
    // set latest available api version instead of specific one
    ant.replaceregexp(match:'/rest/api/3', replace:'/rest/api/latest', flags:'g', byline:false) {
        fileset(dir: '.', includes: 'swagger-v3.v3.json')
    }

    generatorName.set("java")
    inputSpec.set("swagger-v3.v3.json")
    outputDir.set("$buildDir/generated")
    configFile.set("$rootDir/src/main/resources/openapi/config.json")
    ignoreFileOverride = "${projectDir.path}/.openapi-generator-ignore"
    skipOverwrite.set(true)
}

test {
    useJUnitPlatform()
    jacocoTestReport {
        reports {
            xml.required = true
        }
    }
}

build.dependsOn jacocoTestReport

artifacts {
    archives shadowJar
}

sourceSets {
    main {
        resources
                {
                    exclude '**'
                }
    }
}

generatePomFileForShadowPublication { pom.packaging = "jar" }

jar {
    from("src/main/resources") {
        into("/resources")
    }
    from("ui/build") {
        into("/resources")
    }
    manifest {
        attributes(
                "Class-Path": configurations.compileClasspath.collect { it.getName() }.join(' '),
                "Plugin-Id": "${pluginId}",
                "Plugin-Version": "${project.version}",
                "Plugin-Provider": "Report Portal",
                "Plugin-Class": "com.epam.reportportal.extension.jira.CloudJiraPlugin",
                "Plugin-Service": "api"
        )
    }
}

shadowJar {
    from("src/main/resources") {
        into("/resources")
    }
    from("ui/build") {
        into("/resources")
    }
    configurations = [project.configurations.compileClasspath]
    zip64 true
    dependencies {
        include(dependency("jakarta.ws.rs:jakarta.ws.rs-api:.*"))
        include(dependency("org.apache.tomcat:tomcat-annotations-api:.*"))
    }
}

task plugin(type: Jar) {
    getArchiveBaseName().set("plugin-${pluginId}")
    into('classes') {
        with jar
    }
    into('lib') {
        from configurations.compileClasspath
    }
    archiveExtension.set('zip')
}

task assemblePlugin(type: Copy) {
    from plugin
    into pluginsDir
}

task assemblePlugins(type: Copy) {
    dependsOn subprojects.assemblePlugin
}

compileJava.dependsOn npm_run_build, generateJiraApi
